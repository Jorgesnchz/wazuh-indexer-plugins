/*
 * Copyright OpenSearch Contributors
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
  id 'com.github.johnrengelman.shadow'
  id 'maven-publish'
  id 'signing'
  id 'java-library'
}

group = "com.wazuh"
version = "6.0.0.0"
apply plugin: 'opensearch.java'
//apply plugin: 'com.github.johnrengelman.shadow'

shadowJar {
  archiveClassifier.set('all')
}

artifacts {
  archives shadowJar
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url "https://aws.oss.sonatype.org/content/repositories/snapshots" }
}

ext {
  projectSubstitutions = [:]
  licenseFile = rootProject.file('LICENSE.txt')
  noticeFile = rootProject.file('NOTICE')
}

dependencies {
  compileOnly "org.opensearch:opensearch:${opensearch_version}"
  testImplementation "org.apache.logging.log4j:log4j-core:${versions.log4j}"
}

task sourcesJar(type: Jar) {
  archiveClassifier.set 'sources'
  from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
  archiveClassifier.set 'javadoc'
  from javadoc.destinationDir
  dependsOn javadoc
}

publishing {
  repositories {
    maven {
      name = 'staging'
      url = "${rootProject.buildDir}/local-staging-repo"
    }
  }
  publications {
    standard(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar
      pom {
        name = "wazuh-indexer-command-manager-spi"
        packaging = "jar"
        description = "Wazuh Indexer Command Manager SPI"
        groupId = "com.wazuh"
        licenses {
          license {
            name = "The Apache License, Version 2.0"
            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
          }
        }
      }
    }
    shadow(MavenPublication) { publication ->
      project.shadow.component(publication)
      artifact sourcesJar
      artifact javadocJar
      pom {
        name = "wazuh-indexer-command-manager-spi"
        packaging = "jar"
        description = "Wazuh Indexer Command Manager SPI"
        groupId = "com.wazuh"
        licenses {
          license {
            name = "The Apache License, Version 2.0"
            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
          }
        }
      }
    }
  }


  gradle.startParameter.setShowStacktrace(ShowStacktrace.ALWAYS)
  gradle.startParameter.setLogLevel(LogLevel.DEBUG)
}
